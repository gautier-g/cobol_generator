IDENTIFICATION DIVISION.
PROGRAM-ID. PARTICIPATION-DAL-DB.
ENVIRONMENT DIVISION.
DATA DIVISION.
WORKING-STORAGE SECTION.
    EXEC SQL INCLUDE SQLCA END-EXEC.
01  WS-CONNECTED-FLAG      PIC X VALUE 'N'.
01  WS-CURSOR-OPEN-FLAG    PIC X VALUE 'N'.
01  WS-PART-ID-ACTIVITE    PIC 9(9).
01  WS-PART-ID-USER        PIC 9(9).
01  WS-PART-MODE-TRANSPORT PIC S9(2).
01  WS-DB-NAME             PIC X(64).
01  WS-DB-USER             PIC X(64).
01  WS-DB-PASSWORD         PIC X(64).
LINKAGE SECTION.
01 LK-OPERATION PIC X(4).
01 LK-END-OF-FILE PIC X.
01 LK-PARTICIPATION.
    05 LK-PARTICIPATION-ID-ACTIVITE PIC 9(9).
    05 LK-PARTICIPATION-ID-USER PIC 9(9).
    05 LK-PARTICIPATION-MODE-TRANSPORT PIC S9(2).
PROCEDURE DIVISION USING LK-OPERATION LK-END-OF-FILE LK-PARTICIPATION.
MAIN-ENTRY.
    EVALUATE LK-OPERATION
        WHEN 'END '
            PERFORM DAL-END
        WHEN 'READ'
            PERFORM DAL-READ
        WHEN 'SAVE'
            PERFORM DAL-SAVE
        WHEN OTHER
            DISPLAY 'ERREUR: Operation inconnue: ' LK-OPERATION
    END-EVALUATE
    GOBACK.
DAL-CONNECT.
    IF WS-CONNECTED-FLAG = 'Y'
        CONTINUE
    ELSE
        PERFORM DAL-SET-ENV
        EXEC SQL
            CONNECT :WS-DB-USER IDENTIFIED BY :WS-DB-PASSWORD
            USING :WS-DB-NAME
        END-EXEC
        IF SQLCODE = ZERO
            MOVE 'Y' TO WS-CONNECTED-FLAG
            DISPLAY 'Connexion DB reussie'
        ELSE
            DISPLAY 'ERREUR CONNECT: SQLCODE=' SQLCODE
            DISPLAY 'SQLSTATE=' SQLSTATE
            DISPLAY 'SQLERRMC=' SQLERRMC
            MOVE 'N' TO WS-CONNECTED-FLAG
            GOBACK
        END-IF
    END-IF.
DAL-SET-ENV.
    ACCEPT WS-DB-USER FROM ENVIRONMENT 'PGUSER'
    ACCEPT WS-DB-PASSWORD FROM ENVIRONMENT 'PGPASSWORD'
    ACCEPT WS-DB-NAME FROM ENVIRONMENT 'PGDATABASE'.
DAL-END.
    DISPLAY 'Fermeture connexion'
    IF WS-CURSOR-OPEN-FLAG = 'Y'
        EXEC SQL CLOSE C_PAR END-EXEC
        IF SQLCODE NOT = ZERO
            DISPLAY 'ERREUR CLOSE: SQLCODE=' SQLCODE
        END-IF
        MOVE 'N' TO WS-CURSOR-OPEN-FLAG
    END-IF
    IF WS-CONNECTED-FLAG = 'Y'
        EXEC SQL COMMIT END-EXEC
        IF SQLCODE NOT = ZERO
            DISPLAY 'ERREUR COMMIT: SQLCODE=' SQLCODE
        END-IF
        EXEC SQL DISCONNECT ALL END-EXEC
        IF SQLCODE NOT = ZERO
            DISPLAY 'ERREUR DISCONNECT: SQLCODE=' SQLCODE
        END-IF
        MOVE 'N' TO WS-CONNECTED-FLAG
    END-IF.
DAL-READ.
    PERFORM DAL-CONNECT
    IF WS-CURSOR-OPEN-FLAG NOT = 'Y'
        EXEC SQL
            DECLARE C_PAR CURSOR FOR
            SELECT PARTICIPATION_ID_ACTIVITE, PARTICIPATION_ID_USER,
                   PARTICIPATION_MODE_TRANSPORT
            FROM PARTICIPATION
            ORDER BY PARTICIPATION_ID_ACTIVITE, PARTICIPATION_ID_USER
        END-EXEC
        EXEC SQL OPEN C_PAR END-EXEC
        IF SQLCODE = ZERO
            MOVE 'Y' TO WS-CURSOR-OPEN-FLAG
            DISPLAY 'Curseur C_PAR ouvert'
        ELSE
            DISPLAY 'ERREUR OPEN: SQLCODE=' SQLCODE
            GOBACK
        END-IF
    END-IF
    MOVE 'N' TO LK-END-OF-FILE
    EXEC SQL
        FETCH C_PAR INTO
            :WS-PART-ID-ACTIVITE,
            :WS-PART-ID-USER,
            :WS-PART-MODE-TRANSPORT
    END-EXEC
    EVALUATE SQLCODE
        WHEN ZERO
            MOVE WS-PART-ID-ACTIVITE TO
                LK-PARTICIPATION-ID-ACTIVITE OF LK-PARTICIPATION
            MOVE WS-PART-ID-USER TO
                LK-PARTICIPATION-ID-USER OF LK-PARTICIPATION
            MOVE WS-PART-MODE-TRANSPORT TO
                LK-PARTICIPATION-MODE-TRANSPORT OF LK-PARTICIPATION
        WHEN 100
            MOVE 'Y' TO LK-END-OF-FILE
        WHEN OTHER
            DISPLAY 'ERREUR FETCH: SQLCODE=' SQLCODE
            MOVE 'Y' TO LK-END-OF-FILE
    END-EVALUATE.
DAL-SAVE.
    PERFORM DAL-CONNECT
    MOVE LK-PARTICIPATION-ID-ACTIVITE OF LK-PARTICIPATION
        TO WS-PART-ID-ACTIVITE
    MOVE LK-PARTICIPATION-ID-USER OF LK-PARTICIPATION
        TO WS-PART-ID-USER
    MOVE LK-PARTICIPATION-MODE-TRANSPORT OF LK-PARTICIPATION
        TO WS-PART-MODE-TRANSPORT
    EXEC SQL
        UPDATE PARTICIPATION
        SET PARTICIPATION_MODE_TRANSPORT = :WS-PART-MODE-TRANSPORT
        WHERE PARTICIPATION_ID_ACTIVITE = :WS-PART-ID-ACTIVITE
          AND PARTICIPATION_ID_USER = :WS-PART-ID-USER
    END-EXEC
    IF SQLCODE NOT = ZERO
        DISPLAY 'ERREUR UPDATE: SQLCODE=' SQLCODE
    END-IF.