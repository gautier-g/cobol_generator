```
IDENTIFICATION DIVISION.
PROGRAM-ID. EMPLOYEE-DAL-DB.

ENVIRONMENT DIVISION.
DATA DIVISION.
WORKING-STORAGE SECTION.
01  SQLCA.
    05  SQLCAID            PIC X(8).
    05  SQLCABC            PIC S9(9) COMP-5.
    05  SQLCODE            PIC S9(9) COMP-5.
    05  SQLERRM.
        49  SQLERRML       PIC S9(4) COMP-5.
        49  SQLERRMC       PIC X(70).
    05  SQLERRP            PIC X(8).
    05  SQLERRD            OCCURS 6 PIC S9(9) COMP-5.
    05  SQLWARN.
        10  SQLWARN0       PIC X.
        10  SQLWARN1       PIC X.
        10  SQLWARN2       PIC X.
        10  SQLWARN3       PIC X.
        10  SQLWARN4       PIC X.
        10  SQLWARN5       PIC X.
        10  SQLWARN6       PIC X.
        10  SQLWARN7       PIC X.
    05  SQLEXT             PIC X(8).

01  WS-CONNECTED          PIC X VALUE 'N'.
    88  CONNECTED         VALUE 'Y'.
    88  NOT-CONNECTED     VALUE 'N'.

01  WS-CURSOR-OPEN        PIC X VALUE 'N'.
    88  CURSOR-OPEN       VALUE 'Y'.
    88  CURSOR-CLOSED     VALUE 'N'.

LINKAGE SECTION.
01  LK-OPERATION          PIC X(4).
01  LK-END-OF-FILE        PIC X.
01  LK-EMPLOYEE.
    05  LK-EMP-ID         PIC 9(4).
    05  LK-EMP-NAME       PIC A(30).
    05  LK-SALARY-BRUT    PIC 9(6)V99.
    05  LK-SALARY-NET     PIC 9(6)V99.

PROCEDURE DIVISION USING LK-OPERATION LK-END-OF-FILE LK-EMPLOYEE.
    EVALUATE LK-OPERATION
        WHEN 'READ'
            PERFORM DAL-READ
        WHEN 'SAVE'
            PERFORM DAL-SAVE
        WHEN 'END '
            PERFORM DAL-END
    END-EVALUATE
    GOBACK.

DAL-READ.
    PERFORM CONNECT-DB
    PERFORM OPEN-CURSOR
    PERFORM FETCH-EMPLOYEE
    .

CONNECT-DB.
    IF NOT-CONNECTED
        EXEC SQL
            CONNECT TO 'postgres'
        END-EXEC
        IF SQLCODE = 0
            SET CONNECTED TO TRUE
        ELSE
            MOVE 'Y' TO LK-END-OF-FILE
        END-IF
    END-IF
    .

OPEN-CURSOR.
    IF NOT CURSOR-OPEN
        EXEC SQL
            DECLARE C_EMP CURSOR FOR
                SELECT EMP_ID, EMP_NAME, SALARY_BRUT, SALARY_NET
                FROM EMPLOYEE
        END-EXEC
        EXEC SQL
            OPEN C_EMP
        END-EXEC
        IF SQLCODE = 0
            SET CURSOR-OPEN TO TRUE
        ELSE
            MOVE 'Y' TO LK-END-OF-FILE
        END-IF
    END-IF
    .

FETCH-EMPLOYEE.
    EXEC SQL
        FETCH C_EMP INTO
            :LK-EMP-ID,
            :LK-EMP-NAME,
            :LK-SALARY-BRUT,
            :LK-SALARY-NET
    END-EXEC
    IF SQLCODE NOT = 0
        MOVE 'Y' TO LK-END-OF-FILE
    END-IF
    .

DAL-SAVE.
    EXEC SQL
        UPDATE EMPLOYEE
        SET SALARY_NET = :LK-SALARY-NET
        WHERE EMP_ID = :LK-EMP-ID
    END-EXEC
    .

DAL-END.
    PERFORM CLOSE-CURSOR
    PERFORM COMMIT-TRANSACTION
    PERFORM DISCONNECT-DB
    .

CLOSE-CURSOR.
    IF CURSOR-OPEN
        EXEC SQL
            CLOSE C_EMP
        END-EXEC
        SET CURSOR-CLOSED TO TRUE
    END-IF
    .

COMMIT-TRANSACTION.
    EXEC SQL
        COMMIT
    END-EXEC
    .

DISCONNECT-DB.
    IF CONNECTED
        EXEC SQL
            DISCONNECT ALL
        END-EXEC
        SET NOT-CONNECTED TO TRUE
    END-IF
    .
```