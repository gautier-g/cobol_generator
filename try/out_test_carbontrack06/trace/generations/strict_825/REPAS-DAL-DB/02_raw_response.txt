IDENTIFICATION DIVISION.
PROGRAM-ID. REPAS-DAL-DB.
      *****************************************************************
      * Programme: REPAS-DAL-DB                                       *
      * Couche: DAL (Data Access Layer)                               *
      * Role: Acces a la base PostgreSQL pour REPAS                   *
      *       Operations: READ, SAVE, END                             *
      *****************************************************************
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
REPOSITORY.
    FUNCTION ALL INTRINSIC.
DATA DIVISION.
WORKING-STORAGE SECTION.
EXEC SQL INCLUDE SQLCA END-EXEC.
01  WS-CONNECTED-FLAG      PIC X VALUE 'N'.
    88  WS-CONNECTED           VALUE 'Y'.
01  WS-CURSOR-OPEN-FLAG    PIC X VALUE 'N'.
    88  WS-CURSOR-OPEN         VALUE 'Y'.
01  WS-REPAS-ID            PIC 9(9).
01  WS-REPAS-ID-ACTIVITE   PIC 9(9).
01  WS-REPAS-TYPE          PIC 9(2).
01  WS-REPAS-NBREPAS       PIC S9(5).
01  WS-DB-NAME.
    05  WS-DB-NAME-TEXT     PIC X(13) VALUE 'carbontrackdb'.
    05  WS-DB-NAME-TERM     PIC X VALUE X'00'.
01  WS-DB-USER.
    05  WS-DB-USER-TEXT     PIC X(10) VALUE 'carbonuser'.
    05  WS-DB-USER-TERM     PIC X VALUE X'00'.
01  WS-DB-PASSWORD.
    05  WS-DB-PASSWORD-TEXT PIC X(9) VALUE 'CARBONPWD'.
    05  WS-DB-PASSWORD-TERM PIC X VALUE X'00'.
01  WS-DB-NAME-LEN         PIC S9(4) COMP-5 VALUE 13.
01  WS-DB-USER-LEN         PIC S9(4) COMP-5 VALUE 10.
01  WS-DB-PASSWORD-LEN     PIC S9(4) COMP-5 VALUE 9.
01  WS-PGHOST-NAME         PIC X(6) VALUE 'PGHOST'.
01  WS-PGHOST-VALUE        PIC X(64) VALUE 'localhost'.
01  WS-PGPORT-NAME         PIC X(6) VALUE 'PGPORT'.
01  WS-PGPORT-VALUE        PIC X(5) VALUE '5432'.
01  WS-PGUSER-NAME         PIC X(6) VALUE 'PGUSER'.
01  WS-PGUSER-VALUE        PIC X(64) VALUE 'carbonuser'.
01  WS-PGPASSWORD-NAME     PIC X(10) VALUE 'PGPASSWORD'.
01  WS-PGPASSWORD-VALUE    PIC X(64) VALUE 'CARBONPWD'.
01  WS-PGDATABASE-NAME     PIC X(10) VALUE 'PGDATABASE'.
01  WS-PGDATABASE-VALUE    PIC X(64) VALUE 'carbontrackdb'.
LINKAGE SECTION.
01 LK-OPERATION PIC X(4).
01 LK-END-OF-FILE PIC X.
01 LK-REPAS.
    05 LK-REPAS-ID PIC 9(9).
    05 LK-REPAS-ID-ACTIVITE PIC 9(9).
    05 LK-REPAS-TYPE PIC 9(2).
    05 LK-REPAS-NBREPAS PIC S9(5).

PROCEDURE DIVISION USING LK-OPERATION LK-END-OF-FILE LK-REPAS.
MAIN-ENTRY.
    IF LK-OPERATION NOT EQUAL 'END '
        PERFORM DAL-CONNECT
        IF NOT WS-CONNECTED
            MOVE 'Y' TO LK-END-OF-FILE
            GOBACK
        END-IF
    END-IF
    EVALUATE LK-OPERATION
        WHEN 'READ'
            PERFORM DAL-READ
        WHEN 'SAVE'
            PERFORM DAL-SAVE
        WHEN 'END '
            PERFORM DAL-END
        WHEN OTHER
            DISPLAY 'ERREUR: Operation inconnue: ' LK-OPERATION
            MOVE 'Y' TO LK-END-OF-FILE
    END-EVALUATE
    GOBACK.

DAL-SET-ENV.
    DISPLAY WS-PGHOST-NAME UPON ENVIRONMENT-NAME
    DISPLAY WS-PGHOST-VALUE UPON ENVIRONMENT-VALUE
    DISPLAY WS-PGPORT-NAME UPON ENVIRONMENT-NAME
    DISPLAY WS-PGPORT-VALUE UPON ENVIRONMENT-VALUE
    DISPLAY WS-PGUSER-NAME UPON ENVIRONMENT-NAME
    DISPLAY WS-PGUSER-VALUE UPON ENVIRONMENT-VALUE
    DISPLAY WS-PGPASSWORD-NAME UPON ENVIRONMENT-NAME
    DISPLAY WS-PGPASSWORD-VALUE UPON ENVIRONMENT-VALUE
    DISPLAY WS-PGDATABASE-NAME UPON ENVIRONMENT-NAME
    DISPLAY WS-PGDATABASE-VALUE UPON ENVIRONMENT-VALUE
    EXIT PARAGRAPH.

DAL-CONNECT.
    PERFORM DAL-SET-ENV
    CALL "OCESQLStartSQL" END-CALL
    CALL "OCESQLConnect" USING BY REFERENCE SQLCA
                              BY REFERENCE WS-DB-USER
                              BY VALUE WS-DB-USER-LEN
                              BY REFERENCE WS-DB-PASSWORD
                              BY VALUE WS-DB-PASSWORD-LEN
                              BY REFERENCE WS-DB-NAME
                              BY VALUE WS-DB-NAME-LEN
    END-CALL
    CALL "OCESQLEndSQL" END-CALL
    IF SQLCODE EQUAL ZERO
        MOVE 'Y' TO WS-CONNECTED-FLAG
        DISPLAY 'carbontrackdb'
    ELSE
        DISPLAY 'ERREUR CONNECT: SQLCODE=' SQLCODE
        DISPLAY 'SQLSTATE=' SQLSTATE
        DISPLAY 'SQLERRMC=' SQLERRMC
    END-IF
    EXIT PARAGRAPH.

DAL-READ.
    EXEC SQL DECLARE C_REP CURSOR FOR
        SELECT REPAS_ID, REPAS_ID_ACTIVITE, REPAS_TYPE, REPAS_NBREPAS
        FROM REPAS
        ORDER BY REPAS_ID
    END-EXEC
    IF NOT WS-CURSOR-OPEN
        EXEC SQL OPEN C_REP END-EXEC
        IF SQLCODE NOT EQUAL ZERO
            DISPLAY 'ERREUR OPEN: SQLCODE=' SQLCODE
            MOVE 'Y' TO LK-END-OF-FILE
            EXEC SQL ROLLBACK END-EXEC
            EXIT PARAGRAPH
        END-IF
        MOVE 'Y' TO WS-CURSOR-OPEN-FLAG
        DISPLAY 'Curseur C_REP ouvert'
    END-IF
    EXEC SQL FETCH C_REP INTO
        :WS-REPAS-ID,
        :WS-REPAS-ID-ACTIVITE,
        :WS-REPAS-TYPE,
        :WS-REPAS-NBREPAS
    END-EXEC
    IF SQLCODE NOT EQUAL ZERO AND SQLCODE NOT EQUAL 100
        DISPLAY 'ERREUR FETCH: SQLCODE=' SQLCODE
        MOVE 'Y' TO LK-END-OF-FILE
        EXEC SQL ROLLBACK END-EXEC
        EXIT PARAGRAPH
    END-IF
    IF SQLCODE EQUAL 100
        MOVE 'Y' TO LK-END-OF-FILE
        EXIT PARAGRAPH
    END-IF
    MOVE WS-REPAS-ID TO LK-REPAS-ID
    MOVE WS-REPAS-ID-ACTIVITE TO LK-REPAS-ID-ACTIVITE
    MOVE WS-REPAS-TYPE TO LK-REPAS-TYPE
    MOVE WS-REPAS-NBREPAS TO LK-REPAS-NBREPAS
    EXIT PARAGRAPH.

DAL-SAVE.
    MOVE LK-REPAS-ID TO WS-REPAS-ID
    MOVE LK-REPAS-NBREPAS TO WS-REPAS-NBREPAS
    EXEC SQL UPDATE REPAS
        SET REPAS_NBREPAS = :WS-REPAS-NBREPAS
        WHERE REPAS_ID = :WS-REPAS-ID
    END-EXEC
    EVALUATE SQLCODE
        WHEN 0
            CONTINUE
        WHEN OTHER
            DISPLAY 'ERREUR UPDATE: SQLCODE=' SQLCODE
            DISPLAY 'SQLSTATE=' SQLSTATE
            DISPLAY 'SQLERRMC=' SQLERRMC
            EXEC SQL ROLLBACK END-EXEC
            MOVE 'Y' TO LK-END-OF-FILE
    END-EVALUATE
    EXIT PARAGRAPH.

DAL-END.
    IF WS-CURSOR-OPEN
        EXEC SQL CLOSE C_REP END-EXEC
        MOVE 'N' TO WS-CURSOR-OPEN-FLAG
        IF SQLCODE NOT EQUAL ZERO
            DISPLAY 'ERREUR CLOSE: SQLCODE=' SQLCODE
        END-IF
    END-IF
    EXEC SQL COMMIT END-EXEC
    IF SQLCODE NOT EQUAL ZERO
        DISPLAY 'ERREUR COMMIT: SQLCODE=' SQLCODE
    END-IF
    IF WS-CONNECTED
        EXEC SQL DISCONNECT ALL END-EXEC
        MOVE 'N' TO WS-CONNECTED-FLAG
        IF SQLCODE NOT EQUAL ZERO
            DISPLAY 'ERREUR DISCONNECT: SQLCODE=' SQLCODE
        END-IF
    END-IF
    DISPLAY 'Fermeture connexion'
    EXIT PARAGRAPH.