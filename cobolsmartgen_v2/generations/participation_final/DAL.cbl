       IDENTIFICATION DIVISION.
       PROGRAM-ID. ACTIVITY-DAL-DB.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-PC.
       OBJECT-COMPUTER. IBM-PC.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01 WS-CONNECTED           PIC X VALUE 'N'.
       01 WS-CURSOR-OPEN         PIC X VALUE 'N'.
       01 WS-SQLCODE             PIC S9(9) COMP-5.

       EXEC SQL BEGIN DECLARE SECTION END-EXEC.
       01 DBNAME                 PIC X(30) VALUE 'postgres'.
       01 USERNAME               PIC X(30) VALUE 'postgres'.
       01 PASSWD                 PIC X(30) VALUE 'postgres'.

       01 WSUSER.
           05 WSUSERID           PIC 9(9).
           05 WSUSERNOM          PIC X(50).
           05 WSUSERMAIL         PIC X(80).
           05 WSUSERPASS         PIC X(256).
           05 WSUSERROLE         PIC X(15).
           05 WSUSERIDANTENNE    PIC 9(9).
           05 WSUSERLASTLOGIN    PIC 9(18).

       01 WSACTIVITE.
           05 WSACTIVITEID       PIC 9(9).
           05 WSACTIVITENOM      PIC X(50).
           05 WSACTIVITETYPE     PIC X(20).
           05 WSACTIVITEIDANTENNE PIC 9(9).
           05 WSACTIVITEANIMATEUR PIC 9(9).
           05 WSACTIVITENBPART   PIC 9(9).
           05 WSACTIVITEMODETRANS PIC 9(9).
           05 WSACTIVITELIEU     PIC X(100).
           05 WSACTIVITEDISTANCE PIC 9(9).
           05 WSACTIVITEHEBERG   PIC 9(9).
           05 WSACTIVITEREPAS    PIC 9(9).
           05 WSACTIVITEEMPREINTE PIC 9(13)V9(4).

       01 WSPARTICIPATION.
           05 WSPARTACTIVITEID   PIC 9(9).
           05 WSPARTUSERID       PIC 9(9).
           05 WSPARTMODETRANS    PIC 9(9).

       01 WSJOINTURE.
           05 WSJOINTACTIVITEID  PIC 9(9).
           05 WSJOINTACTIVITENOM PIC X(50).
           05 WSJOINTACTIVITETYPE PIC X(20).
           05 WSJOINTACTIVITELIEU PIC X(100).
           05 WSJOINUSERID       PIC 9(9).
           05 WSJOINUSERNOM      PIC X(50).
           05 WSJOINUSERMAIL     PIC X(80).
           05 WSJOINMODETRANS    PIC 9(9).
       EXEC SQL END DECLARE SECTION END-EXEC.

       EXEC SQL INCLUDE SQLCA END-EXEC.

       LINKAGE SECTION.
       01 LK-OPERATION          PIC X(30).
       01 LK-END-OF-FILE        PIC X.
       01 LK-USER.
           05 LKUSERID          PIC 9(9).
           05 LKUSERNOM         PIC X(50).
           05 LKUSERMAIL        PIC X(80).
           05 LKUSERPASS        PIC X(256).
           05 LKUSERROLE        PIC X(15).
           05 LKUSERIDANTENNE   PIC 9(9).
           05 LKUSERLASTLOGIN   PIC 9(18).
       01 LK-ACTIVITE.
           05 LKACTIVITEID      PIC 9(9).
           05 LKACTIVITENOM     PIC X(50).
           05 LKACTIVITETYPE    PIC X(20).
           05 LKACTIVITEIDANTENNE PIC 9(9).
           05 LKACTIVITEANIMATEUR PIC 9(9).
           05 LKACTIVITENBPART  PIC 9(9).
           05 LKACTIVITEMODETRANS PIC 9(9).
           05 LKACTIVITELIEU    PIC X(100).
           05 LKACTIVITEDISTANCE PIC 9(9).
           05 LKACTIVITEHEBERG  PIC 9(9).
           05 LKACTIVITEREPAS   PIC 9(9).
           05 LKACTIVITEEMPREINTE PIC 9(13)V9(4).
       01 LK-PARTICIPATION.
           05 LKPARTACTIVITEID  PIC 9(9).
           05 LKPARTUSERID      PIC 9(9).
           05 LKPARTMODETRANS   PIC 9(9).
       01 LK-JOINTURE.
           05 LKJOINTACTIVITEID PIC 9(9).
           05 LKJOINTACTIVITENOM PIC X(50).
           05 LKJOINTACTIVITETYPE PIC X(20).
           05 LKJOINTACTIVITELIEU PIC X(100).
           05 LKJOINUSERID      PIC 9(9).
           05 LKJOINUSERNOM     PIC X(50).
           05 LKJOINUSERMAIL    PIC X(80).
           05 LKJOINMODETRANS   PIC 9(9).

       PROCEDURE DIVISION USING LK-OPERATION LK-END-OF-FILE
           LK-USER LK-ACTIVITE LK-PARTICIPATION LK-JOINTURE.
       MAIN-LOGIC.
           EVALUATE LK-OPERATION
               WHEN 'CREATEUSER'
                   PERFORM CREATEUSER
               WHEN 'CREATEACTIVITE'
                   PERFORM CREATEACTIVITE
               WHEN 'CREATEPARTICIPATION'
                   PERFORM CREATEPARTICIPATION
               WHEN 'READPARTICIPATION'
                   PERFORM READPARTICIPATION
               WHEN 'LISTPARTBYACT'
                   PERFORM LISTPARTBYACT
               WHEN 'LISTACTBYUSER'
                   PERFORM LISTACTBYUSER
               WHEN 'UPDATEMODETRANS'
                   PERFORM UPDATEMODETRANS
               WHEN 'DELETEPARTICIPATION'
                   PERFORM DELETEPARTICIPATION
               WHEN 'END'
                   PERFORM DALEND
           END-EVALUATE.
           EXIT PROGRAM.

       CONNECTDB.
           IF WS-CONNECTED = 'N'
               EXEC SQL
                   CONNECT :USERNAME IDENTIFIED BY :PASSWD USING :DBNAME
               END-EXEC
               EXEC SQL
                   SET client_encoding TO 'LATIN1'
               END-EXEC
               IF SQLCODE NOT = 0
                   MOVE 'Y' TO LK-END-OF-FILE
                   EXIT PARAGRAPH
               END-IF
               MOVE 'Y' TO WS-CONNECTED
           END-IF.

       CREATEUSER.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKUSERID TO WSUSERID.
           MOVE LKUSERNOM TO WSUSERNOM.
           MOVE LKUSERMAIL TO WSUSERMAIL.
           MOVE LKUSERPASS TO WSUSERPASS.
           MOVE LKUSERROLE TO WSUSERROLE.
           MOVE LKUSERIDANTENNE TO WSUSERIDANTENNE.

           EXEC SQL
               INSERT INTO UTILISATEUR
               (USER_ID, USER_NOM, USER_MAIL, USER_PASS, USER_ROLE,
                USER_ID_ANTENNE)
               VALUES
               (:WSUSERID, :WSUSERNOM, :WSUSERMAIL, :WSUSERPASS,
                :WSUSERROLE, :WSUSERIDANTENNE)
           END-EXEC.

       CREATEACTIVITE.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKACTIVITEID TO WSACTIVITEID.
           MOVE LKACTIVITENOM TO WSACTIVITENOM.
           MOVE LKACTIVITETYPE TO WSACTIVITETYPE.
           MOVE LKACTIVITEIDANTENNE TO WSACTIVITEIDANTENNE.
           MOVE LKACTIVITEANIMATEUR TO WSACTIVITEANIMATEUR.
           MOVE LKACTIVITENBPART TO WSACTIVITENBPART.
           MOVE LKACTIVITEMODETRANS TO WSACTIVITEMODETRANS.
           MOVE LKACTIVITELIEU TO WSACTIVITELIEU.
           MOVE LKACTIVITEDISTANCE TO WSACTIVITEDISTANCE.
           MOVE LKACTIVITEHEBERG TO WSACTIVITEHEBERG.
           MOVE LKACTIVITEREPAS TO WSACTIVITEREPAS.

           EXEC SQL
               INSERT INTO ACTIVITE
               (ACTIVITE_ID, ACTIVITE_NOM, ACTIVITE_TYPE,
                ACTIVITE_IDANTENNE, ACTIVITE_ANIMATEUR,
                ACTIVITE_NBPARTICIPANTS, ACTIVITE_MODETRANSPORT,
                ACTIVITE_LIEU, ACTIVITE_DISTANCE, ACTIVITE_HEBERGEMENT,
                ACTIVITE_REPASPREVU)
               VALUES
               (:WSACTIVITEID, :WSACTIVITENOM, :WSACTIVITETYPE,
                :WSACTIVITEIDANTENNE, :WSACTIVITEANIMATEUR,
                :WSACTIVITENBPART, :WSACTIVITEMODETRANS,
                :WSACTIVITELIEU, :WSACTIVITEDISTANCE,
                :WSACTIVITEHEBERG, :WSACTIVITEREPAS)
           END-EXEC.

       CREATEPARTICIPATION.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKPARTACTIVITEID TO WSPARTACTIVITEID.
           MOVE LKPARTUSERID TO WSPARTUSERID.
           MOVE LKPARTMODETRANS TO WSPARTMODETRANS.

           EXEC SQL
               INSERT INTO PARTICIPATION
               (PARTICIPATION_ID_ACTIVITE, PARTICIPATION_ID_USER,
                PARTICIPATION_MODE_TRANSPORT)
               VALUES
               (:WSPARTACTIVITEID, :WSPARTUSERID, :WSPARTMODETRANS)
           END-EXEC.

       READPARTICIPATION.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKPARTACTIVITEID TO WSPARTACTIVITEID.
           MOVE LKPARTUSERID TO WSPARTUSERID.

           EXEC SQL
               SELECT A.ACTIVITE_ID, A.ACTIVITE_NOM, A.ACTIVITE_TYPE,
                      A.ACTIVITE_LIEU,
                      U.USER_ID, U.USER_NOM, U.USER_MAIL,
                      P.PARTICIPATION_MODE_TRANSPORT
               INTO :WSJOINTACTIVITEID, :WSJOINTACTIVITENOM,
                    :WSJOINTACTIVITETYPE, :WSJOINTACTIVITELIEU,
                    :WSJOINUSERID, :WSJOINUSERNOM, :WSJOINUSERMAIL,
                    :WSJOINMODETRANS
               FROM PARTICIPATION P
               JOIN ACTIVITE A ON 
               P.PARTICIPATION_ID_ACTIVITE = A.ACTIVITE_ID
      *        CORRECTION: RETOUR A LA LIGNE
               JOIN UTILISATEUR U ON P.PARTICIPATION_ID_USER = U.USER_ID
               WHERE P.PARTICIPATION_ID_ACTIVITE = :WSPARTACTIVITEID
               AND P.PARTICIPATION_ID_USER = :WSPARTUSERID
           END-EXEC.

           DISPLAY " SQLSTATE: " SQLSTATE.
           DISPLAY " SQLERRMC: " SQLERRMC.

           IF SQLCODE = 0
               MOVE WSJOINTACTIVITEID TO LKJOINTACTIVITEID
               MOVE WSJOINTACTIVITENOM TO LKJOINTACTIVITENOM
               MOVE WSJOINTACTIVITETYPE TO LKJOINTACTIVITETYPE
               MOVE WSJOINTACTIVITELIEU TO LKJOINTACTIVITELIEU
               MOVE WSJOINUSERID TO LKJOINUSERID
               MOVE WSJOINUSERNOM TO LKJOINUSERNOM
               MOVE WSJOINUSERMAIL TO LKJOINUSERMAIL
               MOVE WSJOINMODETRANS TO LKJOINMODETRANS
           ELSE
               MOVE 'Y' TO LK-END-OF-FILE
           END-IF.

       LISTPARTBYACT.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKACTIVITEID TO WSACTIVITEID.

           IF WS-CURSOR-OPEN = 'N'
               EXEC SQL
                   DECLARE CPARTBYACT CURSOR FOR
                   SELECT A.ACTIVITE_ID, A.ACTIVITE_NOM, 
                          A.ACTIVITE_TYPE,
      *        CORRECTION: RETOUR A LA LIGNE
                          A.ACTIVITE_LIEU,
                          U.USER_ID, U.USER_NOM, U.USER_MAIL,
                          P.PARTICIPATION_MODE_TRANSPORT
                   FROM PARTICIPATION P
                   JOIN ACTIVITE A ON P.PARTICIPATION_ID_ACTIVITE =
                       A.ACTIVITE_ID
                   JOIN UTILISATEUR U ON 
                   P.PARTICIPATION_ID_USER = U.USER_ID
      *        CORRECTION: RETOUR A LA LIGNE
                   WHERE P.PARTICIPATION_ID_ACTIVITE = :WSACTIVITEID
      *        CORRECTION: WSACTIVITEID
               END-EXEC
               EXEC SQL
                   OPEN CPARTBYACT
               END-EXEC
               IF SQLCODE NOT = 0
                   MOVE 'Y' TO LK-END-OF-FILE
                   EXIT PARAGRAPH
               END-IF
               MOVE 'Y' TO WS-CURSOR-OPEN
           END-IF.

           EXEC SQL
               FETCH CPARTBYACT INTO
                   :WSJOINTACTIVITEID, :WSJOINTACTIVITENOM,
                   :WSJOINTACTIVITETYPE, :WSJOINTACTIVITELIEU,
                   :WSJOINUSERID, :WSJOINUSERNOM, :WSJOINUSERMAIL,
                   :WSJOINMODETRANS
           END-EXEC.

           IF SQLCODE = 0
               MOVE WSJOINTACTIVITEID TO LKJOINTACTIVITEID
               MOVE WSJOINTACTIVITENOM TO LKJOINTACTIVITENOM
               MOVE WSJOINTACTIVITETYPE TO LKJOINTACTIVITETYPE
               MOVE WSJOINTACTIVITELIEU TO LKJOINTACTIVITELIEU
               MOVE WSJOINUSERID TO LKJOINUSERID
               MOVE WSJOINUSERNOM TO LKJOINUSERNOM
               MOVE WSJOINUSERMAIL TO LKJOINUSERMAIL
               MOVE WSJOINMODETRANS TO LKJOINMODETRANS
           ELSE
               MOVE 'Y' TO LK-END-OF-FILE
               MOVE 'N' TO WS-CURSOR-OPEN
           END-IF.

       LISTACTBYUSER.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKUSERID TO WSUSERID.

           IF WS-CURSOR-OPEN = 'N'
               EXEC SQL
                   DECLARE CACTBYUSER CURSOR FOR
                   SELECT A.ACTIVITE_ID, A.ACTIVITE_NOM, 
                          A.ACTIVITE_TYPE,
      *        CORRECTION: RETOUR A LA LIGNE
                          A.ACTIVITE_LIEU,
                          U.USER_ID, U.USER_NOM, U.USER_MAIL,
                          P.PARTICIPATION_MODE_TRANSPORT
                   FROM PARTICIPATION P
                   JOIN ACTIVITE A ON P.PARTICIPATION_ID_ACTIVITE =
                       A.ACTIVITE_ID
                   JOIN UTILISATEUR U ON 
                   P.PARTICIPATION_ID_USER = U.USER_ID
                   WHERE P.PARTICIPATION_ID_USER = :WSUSERID
      *        CORRECTION: WSUSERID
               END-EXEC
               EXEC SQL
                   OPEN CACTBYUSER
               END-EXEC
               
               IF SQLCODE NOT = 0
                   MOVE 'Y' TO LK-END-OF-FILE
                   EXIT PARAGRAPH
               END-IF
               MOVE 'Y' TO WS-CURSOR-OPEN
           END-IF.

           EXEC SQL
               FETCH CACTBYUSER INTO
                   :WSJOINTACTIVITEID, :WSJOINTACTIVITENOM,
                   :WSJOINTACTIVITETYPE, :WSJOINTACTIVITELIEU,
                   :WSJOINUSERID, :WSJOINUSERNOM, :WSJOINUSERMAIL,
                   :WSJOINMODETRANS
           END-EXEC.

           IF SQLCODE = 0
               MOVE WSJOINTACTIVITEID TO LKJOINTACTIVITEID
               MOVE WSJOINTACTIVITENOM TO LKJOINTACTIVITENOM
               MOVE WSJOINTACTIVITETYPE TO LKJOINTACTIVITETYPE
               MOVE WSJOINTACTIVITELIEU TO LKJOINTACTIVITELIEU
               MOVE WSJOINUSERID TO LKJOINUSERID
               MOVE WSJOINUSERNOM TO LKJOINUSERNOM
               MOVE WSJOINUSERMAIL TO LKJOINUSERMAIL
               MOVE WSJOINMODETRANS TO LKJOINMODETRANS
           ELSE
               MOVE 'Y' TO LK-END-OF-FILE
           END-IF.

       UPDATEMODETRANS.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKPARTACTIVITEID TO WSPARTACTIVITEID.
           MOVE LKPARTUSERID TO WSPARTUSERID.
           MOVE LKPARTMODETRANS TO WSPARTMODETRANS.

           EXEC SQL
               UPDATE PARTICIPATION
               SET PARTICIPATION_MODE_TRANSPORT = :WSPARTMODETRANS
               WHERE PARTICIPATION_ID_ACTIVITE = :WSPARTACTIVITEID
               AND PARTICIPATION_ID_USER = :WSPARTUSERID
           END-EXEC.
           
       DELETEPARTICIPATION.
           PERFORM CONNECTDB.
           IF LK-END-OF-FILE = 'Y' EXIT PARAGRAPH.

           MOVE LKPARTACTIVITEID TO WSPARTACTIVITEID.
           MOVE LKPARTUSERID TO WSPARTUSERID.

           EXEC SQL
               DELETE FROM PARTICIPATION
               WHERE PARTICIPATION_ID_ACTIVITE = :WSPARTACTIVITEID
               AND PARTICIPATION_ID_USER = :WSPARTUSERID
           END-EXEC.

       DALEND.
           IF WS-CURSOR-OPEN = 'Y'
               EXEC SQL
                   CLOSE CPARTBYACT
               END-EXEC
               EXEC SQL
                   CLOSE CACTBYUSER
               END-EXEC
               MOVE 'N' TO WS-CURSOR-OPEN
           END-IF.

           EXEC SQL
               COMMIT
           END-EXEC.

           IF WS-CONNECTED = 'Y'
               EXEC SQL
                   DISCONNECT ALL
               END-EXEC
               MOVE 'N' TO WS-CONNECTED
           END-IF.